1、hive概念
2、hive的特点和优缺点
3、hive架构
	用户接口
	内部组件
	底层支撑
	元数据库
		描述数据的数据
		为了能够让hive对这份数据进行解析，要对这份数据进行描述
		利用hive提供的类SQL语句（HQL）进行查询分析（OLAP）
4、Hive的数据存储
		库、 表、 分区+分桶 、表文件
	分桶两个特性：
	1、如果一个key的某个值在一个分桶当中，那么所有的key为该值的记录都在这个分桶当中
	2、某些桶中可以有多个key的值，也可以一个都没有。
5、hive的安装

​     重装：
​	1、保证之前的hive数据仓库的文件路径不存在
​	2、保证元数据库不存在

	一套hadoop集群，搭建两个不同的hive仓库
		1、默认仓库路径不一致
		2、元数据库不一致

二、DDL
官网命令链接：  https://cwiki.apache.org/confluence/display/Hive/LanguageManual+DDL
				https://cwiki.apache.org/confluence/display/Hive/LanguageManual+DML
				https://cwiki.apache.org/confluence/display/Hive/LanguageManual+Select

	数据定义语言  Data Definition Language
	（一）针对库
		创建库、查看库、删除库、修改库、切换库
		
	1、创建库
		create database myhive;
		create database if not exists myhive;
	
	CREATE (DATABASE|SCHEMA) [IF NOT EXISTS] database_name
	小括号表示必选，小括号中间的竖线表示或者，中括号表示可选。
		create database myhive_1 comment "myhive_1 database 1" location "/myhive_1";
		location是HDFS的目录
	
	2、查看库
		show databases;//查看所有的库
		select current_database();//查看当前使用的库
		
	3、删除库
		默认：
		drop database myhive_1;	等价于 	drop database myhive_1 RESTRICT;
		drop database myhive_1 cascade;//强制级联删除，慎用！
	
	4、修改库
		基本不常用！！
	
	5、切换库
		use myhive;


​	
	（二）针对表
		创建表、删除表、修改表、查看表、查看某张表的详细信息。
	1、查看表
		show tables;
		show tables in myhive;//在当前库查看另外一个库的表的信息
		show tables "stu*"; 
		
		test_crm_student 
		temp_student
		t_student
	
	2、查看某张表的详细信息
		desc student;
		desc formatted student;
		desc extended student;
	
	3、创建表
		
		分桶表
		示例：CLUSTERED BY department SORTED BY age ASC,id DESC INTO 3 BUCKETS
		
		Create Table ... As Select  简称CTAS
		
		create table studentss like student; //复制表结构，不是复制数据


​		
		struct_type
			: STRUCT < col_name : data_type [COMMENT col_comment], ...>
		id   info             city
		1   "zhangsan",22,AA  beijing
		2	22,33,44          shanghai
		
		22,33,44: array<int>
		"zhangsan",22,AA:struct<name:string,age:int,department:string>


​		
		创建表的类型有6种：
		（1）创建内部表
			create table student(id int, name string, sex string, age int, department string) row format delimited fields terminated by ",";
			
			内部表的类型：MANAGED_TABLE
			
		（2）创建外部表
		create external table student_ext_1(id int, name string, sex string, age int, department string) row format delimited fields terminated by ",";
		
		内部表和外部表的区别：
			删除表的时候，内部表会都删除，外部表只删除元数据
		到底选择内部表还是外部表？
			1、如果数据已经存储在HDFS上面了，需要使用hive去进行分析，并且这份数据还有可能使用其他的计算引擎来执行分析，使用外部表
			2、如果这个一份数据只是hive做数据分析使用，就可以使用内部表
		
		// 指定一个不存在的外部路径: 创建表的时候，会自动给你创建表目录
		create external table student_ext_2(id int, name string, sex string, age int, department string) row format delimited fields terminated by "," location "/student_ext_2";
	
		// 指定一个已经存在的目录: 并且有数据
		//在linux中执行
		hadoop fs -mkdir -p /student_ext_3
		hadoop fs -put /home/data/student.txt /student_ext_3
		//在hive命令行中执行
		create external table student_ext_3(id int, name string, sex string, age int, department string) row format delimited fields terminated by "," location "/student_ext_3";


​		
		（3）创建分区表
		
		// 创建只有一个分区字段的分区表：
		create table student_ptn(id int, name string, sex string, age int, department string) partitioned by (city string comment "partitioned field") row format delimited fields terminated by ",";
	
		load data local inpath "/home/data/student.txt" into table student_ptn;  XXXXXXX
	
		// 把数据导入到一个不存在的分区，它会自动创建该分区
		load data local inpath "/home/data/student.txt" into table student_ptn partition(city="beijing"); 
	
		分区字段是虚拟列，它的值是存储在元数据库中，不是存储在数据文件中。
		
		// 把数据导入到一个已经存在的分区
		alter table student_ptn add partition (city="chongqing");
		load data local inpath "/home/data/student.txt" into table student_ptn partition(city="chongqing");


		// 创建有多个分区字段的分区表：
		create table student_ptn_date(id int, name string, sex string, age int, department string) partitioned by (city string comment "partitioned field", dt string) row format delimited fields terminated by ",";
	
		// 往分区中导入数据:
		load data local inpath "/home/data/student.txt" into table student_ptn_date partition(city="beijing");  //报错
	
		load data local inpath "/home/data/student.txt" into table student_ptn_date partition(city="beijing", dt='2012-12-12');    //正确
	
		// 不能在导入数据的时候指定多个分区定义
		load data local inpath "/home/data/student.txt" into table student_ptn_date partition(city="beijing", dt='2012-12-14') partition (city="beijing" , dt='2012-12-13');   XXXXXX
	
		// 添加分区
		alter table student_ptn_date add partition(city="beijing", dt='2012-12-14') partition (city="beijing" , dt='2012-12-13');   
		alter table student_ptn_date add partition(city="chongqing", dt='2012-12-14') partition (city="chongqing" , dt='2012-12-13'); 
	
		// 查询一个分区表有那些分区
		show partitions student_ptn;
		show partitions student_ptn_date;
		show partitions student;
		
		（4）创建分桶表
		
		// 创建一个分桶表
		create table student_bucket (id int, name string, sex string, age int, department string) clustered by (department) sorted by (age desc, id asc) into 3 buckets row format delimited fields terminated by ",";
		
		Num Buckets:        	3                   	 
		Bucket Columns:     	[department]        	 
		Sort Columns:       	[Order(col:age, order:0), Order(col:id, order:1)]	
		
		你往分通表里面导入数据要通过分桶查询方式进行导入数据。


​		
		（5）从查询语句的结果创建新表
		通过下面的命令：
			create table ... as  select ....
		查询例子：
			select department, count(*) as total from student group by department;
		完整的CTAS语句：
			create table dpt_count as select department, count(*) as total from student group by department;
						
		（6）通过like复制已有表的结构创建新表
		create table student_like like student;
		
	4、删除表
	drop table student;
	drop table if exists student;	
	
	5、修改表
		
		1)修改表名
		alter table student_like rename to studentss;
	
		2)修改字段
	
			添加字段： 
			alter table student2 add columns (city string, dt string);
			删除字段： 
			alter table student2 drop columns (city);   //报错XXXXXXXX
			替换字段：
			alter table student2 replace columns (id int, name string, sex string, age int);
			改变列的定义：
			alter table student2 change id newid string comment "new id";
			改变列的顺序：
			alter table student2 change sex sex string first;
			alter table student2 change name name string after sex;
	
		3)修改分区
	
			添加分区：
			alter table student_ptn add partition(city='tiajin') partition(city='shanghai');
	
			删除分区：
			alter table student_ptn drop partition(city='tiajin');  
			alter table student_ptn drop partition(city='tiajin'),partition(city='shanghai'); 
	
			修改分区的数据目录：
			alter table student_ptn partition(city="beijing") set location "/stu_beijing";   //报错
			alter table student_ptn partition(city="beijing") set location "hdfs://hadoop0:8020/stu_beijing";  //正确


​	
	6、清空表
	truncate table student;
	hadoop fs -rm -r /user/hive/warehouse/myhive.db/student/*


​		


三、DML
	数据操纵语言 Data Manipulation Language
	1、导入数据
	LOAD DATA [LOCAL] INPATH 'filepath' [OVERWRITE] INTO TABLE tablename [PARTITION (partcol1=val1, partcol2=val2 ...)]
	（1）LOAD操作是复制或者移动操作，将数据文件复制或移动到Hive设置的路径上。
	写LOCAL的是复制，不写LOCAL是移动
	load
	load data local inpath "/home/data/student.txt" into table student;
	load data inpath "/student.txt" into table student;

	insert
	insert into table student (id, name, sex, age, department) values (101,"huangbo","M",222,"IT");
	
	创建分区表：
	
	create table student_ptn (id int, name string, sex string, age int) partitioned by (department string) row format delimited fields terminated by ",";


	单重插入：
	insert into table student_ptn partition (department = 'IS') select id,sex,name,age from student where department  = 'IS';
	insert into table student_ptn partition (department = 'CS') select id,sex,name,age from student where department  = 'CS';
	insert into table student_ptn partition (department = 'MA') select id,sex,name,age from student where department  = 'MA';


	多重插入：
	from student 
	insert into table student_ptn partition (department = 'IS') select id,sex,name,age where department = 'IS' 
	insert into table student_ptn partition (department = 'CS') select id,sex,name,age where department = 'CS' 
	insert into table student_ptn partition (department = 'MA') select id,sex,name,age where department = 'MA' 
	
	多重插入最大的好处就是给很多结构相同的SQL语句组合在一起提高所有的HQL的执行效率，翻译成的MapReduce只需要读取一次数据就搞定了。


​	
	2、导出数据
	insert overwrite local directory "/home/data/cs_student" select * from student where department = 'CS';
	
	insert overwrite directory "/home/data/cs_student" select * from student where department = 'CS';


​		
	3、查询数据
	 Hive 中的 SELECT 基础语法和标准 SQL 语法基本一致，支持 WHERE、DISTINCT、GROUP BY、 ORDER BY、HAVING、LIMIT、子查询等